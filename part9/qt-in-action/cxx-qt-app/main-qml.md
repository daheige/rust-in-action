# main-qml逻辑说明

这个cxx-qt-app桌面应用程序对应的main.qml文件内容如下：

```qmllang
// qml/main.qml 文件定义

// 引入qt基本组件
import QtQuick 2.12;
import QtQuick.Controls 2.12;
import QtQuick.Window 2.12;
import QtQuick.Controls.Basic 2.12;

// 这里导入的包名必须要 build.rs文件中的uri:"cxx_qt.myapp"一致
import cxx_qt.myapp 1.0;

// 定义窗口应用程序
Window {
    title: qsTr("cxx-qt-app")
    visible: true
    height: 480
    width: 640
    color: "#e4af79"

    Hello {
        // id表示每个对象元素的唯一标识
        id: hello
        plain: "" // plain是一个QString字符串类型
    }

    RandObj {
        id: rand_obj
        number: 0 // number是一个i32数字类型,对应rust的i32数字类型
        number2: 0 // number2是一个i32数字类型，对应js的number数字类型
    }

    Column {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.verticalCenter: parent.verticalCenter
        /* space between widget */
        spacing: 10

        // 点击Say Hello按钮下方显示 hello,world
        Button {
            text: "Say Hello"
            onClicked: {
                // 支持js es5/es6语法
                console.log("call say_hello from rust cxx-qt-app");
                // 调用Hello上面的say_hello方法，这里的say_hello需要采用驼峰格式
                let s = hello.sayHello();
                // 将结果赋值给hello.plain字段
                hello.plain = s;
            }
        }

        Label {
            text: hello.plain
            font.bold: true
        }

        // 通过rust生成一个随机数
        Button {
            text: "gen a rand number by rust"
            onClicked: {
                // qt qml文件支持js语法，也就是说支持js es5/es6语法
                let rnd = rand_obj.genNumber(1, 101);
                console.log("gen js random number: ", rnd);
                rand_obj.number = rnd;
            }
        }

        Label {
            text: "the random number generated by rust is: " + rand_obj.number
        }

        // 通过js提供的Math.random函数生成一个随机数
        Button {
            text: "gen a rand number by js"
            onClicked: {
                // 生成1-100的随机数字
                let m = 1;
                let n = 100;
                let rnd = Math.floor(Math.random() * (n - m)) + m;
                console.log("gen js random number: ", rnd);
                rand_obj.number2 = rnd
            }
        }

        Label {
            text: "the random number generated by js is: " + rand_obj.number2
        }
    }
}
```

- 在这个main.qml文件中，首先引入了Qt框架QtQuick下的基本组件，然后通过import引入了cxx_qt.myapp，这个包名和build.rs文件中定义的uri包名必须一致，否则在应用程序编译时就会报错。
- 接着，在Window对象中定义了Hello和RandObj对象。这里需要注意的一点：Window对象下的Hello和RandObj必须和src/cxxqt_object.rs文件中定义Hello和RandObj结构体名字一样。
- 随后，在Column对象中定义了Button按钮对象，按钮的文本是"Say Hello"。在这个Button上面绑定了OnClicked事件。
- 这个OnClicked实际上就是一个function，在这个function里面调用hello.sayHello方法，这个方法是cxxqt_object.rs中qobject提供的外部函数接口say_hello。
- 在这个Column对象中，接着定义了另一个Button按钮对象，并绑定了OnClicked事件，在该事件中调用rand_obj.genNumber函数生成1-100之间的随机数字。
- 由于Qt框架的qml支持js语法，所以我在Column对象中还定义了一个Button按钮对象，并绑定了OnClicked事件，在该事件中通过Math.random生成了1-100之间的随机数字。
